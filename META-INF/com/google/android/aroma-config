ini_set("rom_name",             "Panda Kernel");
ini_set("rom_version",          "@BUILD_VERSION@");
ini_set("rom_author",           "Pzqqt");
ini_set("rom_device",           "Whyred");
ini_set("rom_date",             "@BUILD_DATE@");

ini_set("force_colorspace", "rgba");

fontresload("0", "ttf/VonwaonBitmap-16pxLite.ttf", "12");
fontresload("1", "ttf/VonwaonBitmap-16pxLite.ttf", "16"); 

theme("default");

setvar("random_num", resexec("random_num.sh"));

# 2/5
cmp(getvar("random_num"), ">=", "0") && cmp(getvar("random_num"), "<=", "3") && splash(1500, "splash");
# 2/5
cmp(getvar("random_num"), ">=", "4") && cmp(getvar("random_num"), "<=", "7") && splash(1500, "splash_2");
# 1/5
cmp(getvar("random_num"), ">=", "8") && splash(2000, "splash_3");

menubox(
    "\n<@center>Select Language</@>",
    "<@center>\n选择安装器语言\n\n\n\n</@>",
    "",
    "lang.prop",
    "English", "", "",
    "中文 (简体)", "", ""
);

if prop("lang.prop", "selected") == "1" then
    loadlang("langs/en.lang");
    setvar("pa_version",      "Version");
    setvar("pa_author",       "Author\t");
    setvar("pa_release",      "Release");
    setvar("pa_press_next",   "Press Next to continue ...");
    setvar("pa_error",        "Error!");
    setvar("pa_warning",      "Warning!");
    setvar("pa_ok",           "OK");
    setvar("pa_done",         "Done");
    setvar("pa_plswait",      "Please wait...");

    setvar("pa_wrong_device", "This kernel is only for Redmi Note 5 Pro (whyred) devices!");

    setvar("pa_oc",           "Need CPU overclock?");
    setvar("pa_oc_1",         "Note: Choose \"yes\" if you need best performance\n& don't care about CPU life and battery life.\n\nBeware of device explosion!");
    setvar("pa_oc_s1",        "Yes (big: 2208 MHz, LITTLE: 1843 MHz)");
    setvar("pa_oc_s2",        "No (Stock, recommend)");

    setvar("pa_uv",           "Need UV?");
    setvar("pa_uv_1",         "Note: Reduce the voltage can save battery,\nbut may also cause the device instability!");
    setvar("pa_uv_s1",        "No, please keep default voltage.");
    setvar("pa_uv_s2",        "Yes, I need it!");
    setvar("pa_uv_warning",   "If the device is damaged or data is lost due to UV, developer of Panda Kernel is not responsible for it!");

    setvar("pa_cpu_pow_uv",   "Select UV level of the power cores");
    setvar("pa_cpu_pow_uv_1", "Note: The power cores (LITTLE) focuses more on battery saving, it's suitable for the daily tasks of the device.");
    setvar("pa_cpu_perf_uv",  "Select UV level of the performance cores");
    setvar("pa_cpu_perf_uv_1","Note: The performance cores (big) focuses more on performance, it's suitable for gaming.");
    setvar("pa_gpu_uv",       "Select UV level of GPU");
    setvar("pa_gpu_uv_1",     "Note: GPU is for processing graphics. You know...");
    setvar("pa_uv_lv0",       "No UV (default)");
    setvar("pa_uv_lv1",       "20 mV");
    setvar("pa_uv_lv2",       "40 mV");
    setvar("pa_uv_lv3",       "80 mV (Danger!)");
    setvar("pa_uv_lv4",       "120 mV (Very Danger!)");
    setvar("pa_uv_lv5",       "160 mV (BROKEN!)");

    setvar("pa_cam",          "Select camera blobs");
    setvar("pa_cam_1",        "Note: If the camera not working after flashing,\nplease reflash this kernel & select other option here.");
    setvar("pa_cam_s1",       "NEW camera blobs");
    setvar("pa_cam_s2",       "OLD camera blobs");
    setvar("pa_cam_s3",       "Auto detection (Maybe inaccurate)");

    setvar("pa_hbm",          "Select wired headphone buttons mode");
    setvar("pa_hbm_1",        "Note: The default is \"Stock mode\".\n\nIf the wired headphone volume buttons not working or response abnormal after flashing, please reflash this kernel & select \"Alternative mode\" here.");
    setvar("pa_hbm_s1",       "Stock mode");
    setvar("pa_hbm_s2",       "Alternative mode");

    setvar("pa_em",           "Select EAS energy model");
    setvar("pa_em_1",         "Note: The default is \"CAF\" energy model.\nKeep default if you don't know about energy model.");
    setvar("pa_em_s1",        "CAF (From Qualcomm, designed for sdm660)");
    setvar("pa_em_s2",        "Kdrag0n (Calculated for sdm660)");
    setvar("pa_em_s3",        "Kdrag0n (Calculated for sdm636)");
    setvar("pa_em_s3_sub",    "Note: Not available if you enabled overclock");
    setvar("pa_em_s4",        "Hypeartist (Calculated by hypeartist)");
    setvar("pa_em_s_na",      "This energy model is not available,\nplease select another.");

    setvar("pa_qh",           "Select haptic driver version");
    setvar("pa_qh_1",         "Note: The default is \"QPNP Haptic\".\n\nIf the vibration not working after flashing,\nplease reflash this kernel & select another option here.");
    setvar("pa_qh_s1",        "QPNP Haptic (For most roms)");
    setvar("pa_qh_s2",        "QTI Haptics (For a few roms with requirements)");

    setvar("pa_ef",           "Want to try efficient CPU frequencies?");
    setvar("pa_ef_1",         "Note: The efficient frequencies scheme abandons those lower CPU frequency gears. This will not make your device more power saving, but you can get best performance and fluency when screen on.");
    setvar("pa_ef_s1",        "Yes, I want to try");
    setvar("pa_ef_s2",        "No, I don't (default)");

    setvar("pa_ready",        "Ready to install");
    setvar("pa_ready_1",      "Please confirm");
    setvar("pa_ready_s1",     "Install ");
    setvar("pa_ready_s2",     "Abort installation");
    setvar("pa_wipe_cache_t", "Wipe dalvik-cache & cache");
    setvar("pa_wipe_cache_w", "Do not do this if you don't know\nwhat are you doing!\n\nContinue?");
    setvar("pa_wipe_cache_d", "Note: The next boot may take 1~10 minutes,\nbecause system needs to rebuild dalvik-cache.\nPlease wait patiently.");

    setvar("pa_installing",   "Installing");
    setvar("pa_installing_1", "Installing Panda Kernel\nPlease wait");
    setvar("pa_ins_finish",   "Installation Completed");
    setvar("pa_ins_finish_1", "Congratulations!");
    setvar("pa_ins_finish_2", "has been installed into your device.");
    setvar("pa_reboot",       "Reboot your device now");
    setvar("pa_ins_failed",   "Installation Failed");
    setvar("pa_exit_code",    "Installer Status: ");
    setvar("pa_finish",       "Finish");
    setvar("pa_text_yes",     "Take the risk");
    setvar("pa_text_yes_s",   "Yes");
    setvar("pa_text_no",      "Give up");
    setvar("pa_text_no_s",    "No");
endif;

if prop("lang.prop", "selected") == "2" then
    loadlang("langs/cn.lang");
    setvar("pa_version",      "版本");
    setvar("pa_author",       "作者");
    setvar("pa_release",      "发布");
    setvar("pa_press_next",   "点击下一步继续...");
    setvar("pa_error",        "错误!");
    setvar("pa_warning",      "警告");
    setvar("pa_ok",           "好的");
    setvar("pa_done",         "完成");
    setvar("pa_plswait",      "请稍侯...");

    setvar("pa_wrong_device", "该内核仅适用于红米 Note 5 (whyred) 设备!");

    setvar("pa_oc",           "你需要 CPU 超频吗?");
    setvar("pa_oc_1",         "注意: 如果需要最佳性能并且\n不在乎 CPU 寿命和电池续航的话, 可以考虑选择\"是\".\n\n当心设备爆炸!");
    setvar("pa_oc_s1",        "是 (大核: 2208 MHz, 小核: 1843 MHz)");
    setvar("pa_oc_s2",        "否 (默认频率, 推荐)");

    setvar("pa_uv",           "你需要降压吗?");
    setvar("pa_uv_1",         "注意: 适当降压可以省电, 但是也可能会导致设备不稳定!");
    setvar("pa_uv_s1",        "否, 保持默认电压.");
    setvar("pa_uv_s2",        "是, 我需要降压!");
    setvar("pa_uv_warning",   "如因降压导致设备损坏或数据丢失, Panda 内核的开发者不负任何责任!");

    setvar("pa_cpu_pow_uv",   "选择能效核心 (小核) 的降压等级");
    setvar("pa_cpu_pow_uv_1", "注意: 能效核心 (小核) 更注重于节能, 适用于日常任务.");
    setvar("pa_cpu_perf_uv",  "选择性能核心 (大核) 的降压等级");
    setvar("pa_cpu_perf_uv_1","注意: 性能核心 (大核) 更注重于性能, 适用于游戏.");
    setvar("pa_gpu_uv",       "选择 GPU 的降压等级");
    setvar("pa_gpu_uv_1",     "注意: GPU 用于处理图形. 你懂的...");
    setvar("pa_uv_lv0",       "不降压 (默认)");
    setvar("pa_uv_lv1",       "20 mV");
    setvar("pa_uv_lv2",       "40 mV");
    setvar("pa_uv_lv3",       "80 mV (危险!)");
    setvar("pa_uv_lv4",       "120 mV (非常危险!)");
    setvar("pa_uv_lv5",       "160 mV (可能导致无法进入系统!)");

    setvar("pa_cam",          "选择相机 blobs 版本");
    setvar("pa_cam_1",        "注意: 如果刷入内核之后相机出现问题,\n请重刷本内核并在此选择其他选项.");
    setvar("pa_cam_s1",       "新版本");
    setvar("pa_cam_s2",       "旧版本");
    setvar("pa_cam_s3",       "自动检测 (可能不准确)");

    setvar("pa_hbm",          "选择耳机按键模式");
    setvar("pa_hbm_1",        "注意: 默认为普通模式,\n如果刷入内核后有线耳机音量按键失灵或异常,\n请重刷本内核并在此选择\"特殊模式\".");
    setvar("pa_hbm_s1",       "普通模式");
    setvar("pa_hbm_s2",       "特殊模式");

    setvar("pa_em",           "选择 EAS 调度能量模型");
    setvar("pa_em_1",         "注意: 默认为\"CAF\"的能量模型,\n如果你不了解能量模型, 请保持默认.");
    setvar("pa_em_s1",        "CAF (来自高通官方为 sdm660 设计的能量模型)");
    setvar("pa_em_s2",        "Kdrag0n (由 kdrag0n 为 sdm660 计算的能量模型)");
    setvar("pa_em_s3",        "Kdrag0n (由 kdrag0n 为 sdm636 计算的能量模型)");
    setvar("pa_em_s3_sub",    "注意: 若选择超频, 则该能量模型不可用");
    setvar("pa_em_s4",        "Hypeartist (由 hypeartist 计算的能量模型)");
    setvar("pa_em_s_na",      "该能量模型不可用, 请选择其他选项.");

    setvar("pa_qh",           "选择 haptic 驱动版本");
    setvar("pa_qh_1",         "注意: 默认为\"QPNP Haptic\".\n\n如果刷入内核之后震动出现问题,\n请重刷本内核并在此选择另一个选项.");
    setvar("pa_qh_s1",        "QPNP Haptic (适用于绝大多数 rom)");
    setvar("pa_qh_s2",        "QTI Haptics (适用于个别有要求的 rom)");

    setvar("pa_ef",           "你想试试高频方案吗?");
    setvar("pa_ef_1",         "注意: 高频方案抛弃了那些较低的 CPU 频率挡位.\n这样并不能使你的设备更加省电,\n但是在亮屏时能够得到最佳的性能和流畅度.");
    setvar("pa_ef_s1",        "我想试试");
    setvar("pa_ef_s2",        "我不想 (默认)");

    setvar("pa_ready",        "即将开始安装");
    setvar("pa_ready_1",      "请确认");
    setvar("pa_ready_s1",     "确认安装 ");
    setvar("pa_ready_s2",     "终止安装程序");
    setvar("pa_wipe_cache_t", "清除 dalvik-cache & cache");
    setvar("pa_wipe_cache_w", "如果你不知道自己在做什么, 请不要这样做!\n\n确定要继续吗?");
    setvar("pa_wipe_cache_d", "注意: 下次开机可能会花费 1~10 分钟,\n因为系统需要重建 dalvik-cache.\n请耐心等待.");

    setvar("pa_installing",   "正在安装");
    setvar("pa_installing_1", "正在安装 Panda 内核\n请稍候");
    setvar("pa_ins_finish",   "安装完成");
    setvar("pa_ins_finish_1", "完成!");
    setvar("pa_ins_finish_2", "已成功安装.");
    setvar("pa_reboot",       "现在重启?");
    setvar("pa_ins_failed",   "安装失败");
    setvar("pa_exit_code",    "安装状态：");
    setvar("pa_finish",       "完成");
    setvar("pa_text_yes",     "我愿意承担风险");
    setvar("pa_text_yes_s",   "同意");
    setvar("pa_text_no",      "还是算了吧");
    setvar("pa_text_no_s",    "放弃");
endif;

viewbox(
    "\n<@center>" + ini_get("rom_name") + "</@>",
    "<@center>\n" + ini_get("rom_device") + "\n\n</@>" +
    "\n\n\n\n  " + getvar("pa_version") + " \t: <#selectbg_g>" + ini_get("rom_version") + "</#>\n" +
    "  " + getvar("pa_author") + " \t: <#selectbg_g>" + ini_get("rom_author") + "</#>\n" +
    "  " + getvar("pa_release") + " \t: <#selectbg_g>" + ini_get("rom_date") + "</#>\n\n\n" +
    getvar("pa_press_next"),
    ""
);

if resexec("is_whyred.sh") != "0" then
    alert(
        getvar("pa_error"),
        getvar("pa_wrong_device"),
        "@trash",
        getvar("pa_ok")
    );
    exit("");
endif;

selectbox(
    "\n\n<@center>" + getvar("pa_oc") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_oc_1") + "\n</@>",
    "",
    "is_oc.prop",
    getvar("pa_oc_s1"), "", 0,
    getvar("pa_oc_s2"), "", 1
);

selectbox(
    "\n\n<@center>" + getvar("pa_uv") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_uv_1") + "\n</@>",
    "",
    "uv_confirm.prop",
    getvar("pa_uv_s1"), "", 1,
    getvar("pa_uv_s2"), "", 0
);
if prop("uv_confirm.prop", "selected.0") == "2" then
    if confirm(
        getvar("pa_warning"),
        getvar("pa_uv_warning"),
        "@warning",
        getvar("pa_text_yes"),
        getvar("pa_text_no")) != "yes"
    then
        back("1");
    endif;
    selectbox(
        "\n\n<@center>" + getvar("pa_cpu_pow_uv") + "</@>",
        "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_cpu_pow_uv_1") + "\n</@>",
        "",
        "cpu_pow_uv_level.prop",
        getvar("pa_uv_lv0"), "", 1,
        getvar("pa_uv_lv1"), "", 0,
        getvar("pa_uv_lv2"), "", 0,
        getvar("pa_uv_lv3"), "", 0,
        getvar("pa_uv_lv4"), "", 0,
        getvar("pa_uv_lv5"), "", 0
    );
    selectbox(
        "\n\n<@center>" + getvar("pa_cpu_perf_uv") + "</@>",
        "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_cpu_perf_uv_1") + "\n</@>",
        "",
        "cpu_perf_uv_level.prop",
        getvar("pa_uv_lv0"), "", 1,
        getvar("pa_uv_lv1"), "", 0,
        getvar("pa_uv_lv2"), "", 0,
        getvar("pa_uv_lv3"), "", 0,
        getvar("pa_uv_lv4"), "", 0,
        getvar("pa_uv_lv5"), "", 0
    );
    selectbox(
        "\n\n<@center>" + getvar("pa_gpu_uv") + "</@>",
        "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_gpu_uv_1") + "\n</@>",
        "",
        "gpu_uv_level.prop",
        getvar("pa_uv_lv0"), "", 1,
        getvar("pa_uv_lv1"), "", 0,
        getvar("pa_uv_lv2"), "", 0,
        getvar("pa_uv_lv3"), "", 0,
        getvar("pa_uv_lv4"), "", 0,
        getvar("pa_uv_lv5"), "", 0
    );
endif;

selectbox(
    "\n\n<@center>" + getvar("pa_cam") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_cam_1") + "\n</@>",
    "",
    "is_fixcam.prop",
    getvar("pa_cam_s1"), "", 0,
    getvar("pa_cam_s2"), "", 0,
    getvar("pa_cam_s3"), "", 1
);

selectbox(
    "\n\n<@center>" + getvar("pa_hbm") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_hbm_1") + "\n</@>",
    "",
    "headphone_buttons_mode.prop",
    getvar("pa_hbm_s1"), "", 1,
    getvar("pa_hbm_s2"), "", 0
);

selectbox(
    "\n\n<@center>" + getvar("pa_em") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_em_1") + "\n</@>",
    "",
    "energy_model.prop",
    getvar("pa_em_s1"), "", 1,
    getvar("pa_em_s2"), "", 0,
    getvar("pa_em_s3"), getvar("pa_em_s3_sub"), 0,
    getvar("pa_em_s4"), "", 0
);

if prop("is_oc.prop", "selected.0") == "1" && prop("energy_model.prop", "selected.0") == 3 then
    alert(
        getvar("pa_error"),
        getvar("pa_em_s_na"),
        "@trash",
        getvar("pa_ok")
    );
    back(1);
endif;

selectbox(
    "\n\n<@center>" + getvar("pa_qh") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_qh_1") + "\n</@>",
    "",
    "qti_haptics.prop",
    getvar("pa_qh_s1"), "", 1,
    getvar("pa_qh_s2"), "", 0
);

selectbox(
    "\n\n<@center>" + getvar("pa_ef") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_ef_1") + "\n</@>",
    "",
    "efficient_frequencies.prop",
    getvar("pa_ef_s1"), "", 0,
    getvar("pa_ef_s2"), "", 1
);

menubox(
    "\n\n<@center>" + getvar("pa_ready") + "</@>",
    "<@center>\n\n\n\n\n\n\n\n" + getvar("pa_ready_1") + "\n</@>",
    "",
    "menu.prop",
    getvar("pa_ready_s1") + ini_get("rom_name") + " " + ini_get("rom_version"), "", "@install",
    getvar("pa_ready_s2"), "", "@trash",
    getvar("pa_wipe_cache_t"), "", "@action",
    " ", "", ""
);

prop("menu.prop", "selected") == "2" && exit("");

if prop("menu.prop", "selected") == "3" then
    if confirm(
        getvar("pa_warning"),
        getvar("pa_wipe_cache_w"),
        "@warning") == "yes"
    then
        pleasewait(getvar("pa_plswait"));
        resexec("wipe_dalvik_cache.sh");
        alert(
            getvar("pa_done"),
            getvar("pa_wipe_cache_d"),
            "@done",
            getvar("pa_ok")
        );
    endif;
    back("1");
endif;

if prop("menu.prop", "selected") == "4" then
    if readtmpfile("darkness") == "4" then
        writetmpfile("darkness", "0");
        textbox(
            "Meanwhile...",
            "Abandoned in the void of nothingness",
            "",
            resread("Darkness.txt")
        );
        back("2");
    else
        writetmpfile("darkness", cal(readtmpfile("darkness") || "0", "+", "1"));
    endif;
    back("1");
endif;

setvar("retstatus", install(
    "<@center>\n" + getvar("pa_installing") + "</@>",
    "<@center>\n" + getvar("pa_installing_1") + "</@>",
    "")
);

ini_set("text_next", getvar("pa_finish"));

if getvar("retstatus") == "0" then
    checkviewbox(
        getvar("pa_ins_finish"),
        "<#selectbg_g>" + getvar("pa_ins_finish_1") + "</#>\n\n" +
        "" + ini_get("rom_name") + " " + getvar("pa_ins_finish_2") + "\n\n" +
        getvar("pa_exit_code") + getvar("retstatus") + "\n\n",
        "@welcome",
        getvar("pa_reboot"),
        "0",
        "reboot_it"
    );
else
    viewbox(
        getvar("pa_ins_failed"),
        "\n" + getvar("pa_exit_code") + getvar("retstatus"),
        "@warning"
    );
endif;

getvar("reboot_it") == "1" && reboot("onfinish");
